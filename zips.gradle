ext.licenseFile = new File("$rootDir/license.txt")

task cppSourcesZip(type: Zip) {
    destinationDir = project.buildDir
    classifier = "sources"

    from(licenseFile) {
        into '/'
    }

    from('src/main/native/cpp') {
        into '/'
    }

    model {
        tasks {
            it.each {
                if (it.name.contains('jniHeaders')) {
                    from (it.outputs.files) {
                        into '/'
                    }
                    dependsOn it
                }
            }
        }
    }
}

task cppHeadersZip(type: Zip) {
    destinationDir = project.buildDir
    classifier = "headers"

    from(licenseFile) {
        into '/'
    }

    from('src/main/native/include') {
        into '/'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives cppHeadersZip
    archives cppSourcesZip
}

def getComponentConfigs = { components, name ->
    def configMap = [:]
    components.each {
        if (it in NativeLibrarySpec && it.name == name) { 
            it.binaries.each {
                def target = getClassifier(it)
                if (configMap.containsKey(target)) {
                    configMap.get(target).add(it)
                } else {
                    configMap.put(target, [])
                    configMap.get(target).add(it)
                }
            }
        }
    }
    return configMap
}

def createComponentZipTasks = { configs, baseName, type, func ->
    def taskList = []
    configs.each { key, value ->
        def task = project.tasks.create(baseName + "-${key}", type) {
            description = 'Creates component archive for platform ' + key
            destinationDir =  project.buildDir
            classifier = key
            baseName = baseName
            duplicatesStrategy = 'exclude'

            from(licenseFile) {
                into '/'
            }

            func(it, value)
        }
        taskList.add(task)
    }
    return taskList
}



model {
    publishing {
        def ntcoreConfigMap = getComponentConfigs($.components, 'ntcore')
        def ntcoreTaskList = createComponentZipTasks(ntcoreConfigMap, 'zipcppntcore', Zip, { task, value ->
            value.each { binary->
                if (binary.buildable) {
                    if (binary instanceof SharedLibraryBinarySpec) {
                        task.dependsOn binary.buildTask
                        task.from(new File(binary.sharedLibraryFile.absolutePath + ".debug")) {
                            into getPlatformPath(binary) + '/shared'
                        }
                        task.from (binary.sharedLibraryFile) {
                            into getPlatformPath(binary) + '/shared'
                        }
                        task.from (binary.sharedLibraryLinkFile) {
                            into getPlatformPath(binary) + '/shared'
                        }
                    } else if (binary instanceof StaticLibraryBinarySpec) {
                        task.dependsOn binary.buildTask
                        task.from (binary.staticLibraryFile) {
                            into getPlatformPath(binary) + '/static'
                        }
                    }
                }
            }
        })

        

        def ntcoreJNIConfigMap = getComponentConfigs($.components, 'ntcoreJNI')
        def ntcoreJNITaskList = createComponentZipTasks(ntcoreJNIConfigMap, 'jnijnintcore', Jar, { task, value ->
            value.each { binary->
                if (binary.buildable) {
                    if (binary instanceof SharedLibraryBinarySpec) {
                        task.dependsOn binary.buildTask
                        task.from (binary.sharedLibraryFile) {
                            into getPlatformPath(binary)
                        }
                    }
                }
            }
        })

        ntcoreTaskList.each {
            project.artifacts {
                it
            }
            build.dependsOn it
        }

        ntcoreJNITaskList.each {
            project.artifacts {
                it
            }
            build.dependsOn it
        }
        publications {
            cpp(MavenPublication) {
                ntcoreTaskList.each {
                    artifact it
                }
                artifact cppHeadersZip
                artifact cppSourcesZip

                artifactId = "${baseArtifactId}-cpp"
                groupId artifactGroupId
                version pubVersion
            }
            jni(MavenPublication) {
                ntcoreJNITaskList.each {
                    artifact it
                }

                artifactId = "${baseArtifactId}-jni"
                groupId artifactGroupId
                version pubVersion
            }
        }
    }
}